package org.sw.lesson03.demo3;

import org.sw.facts.Application;
import org.sw.facts.ClientID;
import org.sw.facts.Status;
import java.time.LocalDate;

rule "Invalidate Applications with missing ID"
    dialect "java"
    when
        $application: Application( status != Status.DENIED )
        not ClientID( idNumber == $application.getClientIdNumber())
    then
        System.out.println($application + " Invalid – No matching ID found");
        modify($application) {
            setStatus(Status.DENIED)
        }
end

rule "Invalidate applications with incorrect name"
    dialect "java"
    when
        $application: Application( status != Status.DENIED )
        $clientID: ClientID( idNumber == $application.getClientIdNumber(), name != $application.clientName )
    then
        System.out.println($application + " Invalid – Name [" + $application.getClientName() + "] not matching name on ID [" + $clientID.getName() + "]");
        modify($application) {
            setStatus(Status.DENIED)
        }
end

rule "Invalidate Applications with Expired ID"
    dialect "java"
    when
        $application: Application( status != Status.DENIED )
        $clientid: ClientID( idNumber == $application.getClientIdNumber(),
                             expiryDate.isBefore(LocalDate.now()) )
    then
        System.out.println($application + " Invalid – " + $clientid + " ID expired");
        modify($application) {
            setStatus(Status.DENIED)
        }
end