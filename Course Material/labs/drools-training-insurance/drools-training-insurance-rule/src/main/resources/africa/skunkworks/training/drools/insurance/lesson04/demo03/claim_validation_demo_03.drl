package africa.skunkworks.training.drools.insurance.lesson03.demo03;

import africa.skunkworks.training.drools.insurance.domain.dto.ClaimDto;
import africa.skunkworks.training.drools.insurance.domain.dto.PolicyDto;
import africa.skunkworks.training.drools.insurance.domain.enums.Status
import africa.skunkworks.training.drools.insurance.domain.dto.Payout;

dialect  "java"

rule "Valid Claims"
    agenda-group "valid-claim"
    when
        $claim: ClaimDto( status == Status.PENDING )
    then
    modify($claim) {
        setReason($claim + ": not invalidated. Approval Granted"),
        setStatus(Status.APPROVED)
    }
end

rule "Invalidate claim without matching policy"
    agenda-group "invalid-claim"
    when
        $claim: ClaimDto( status != Status.DENIED )
        not PolicyDto( policyNumber == $claim.getPolicyNumber() )
    then
        modify($claim) {
            setReason($claim + ": failed (no matching policy)"),
            setStatus(Status.DENIED)
        }
end

rule "Invalidate claim that exceeds coverage"
    dialect "java"
    agenda-group "invalid-claim"
    when
        $claim: ClaimDto( status != Status.DENIED )
        $policy: PolicyDto( policyNumber == $claim.getPolicyNumber(), coverage < $claim.getClaimAmount() )
    then
        System.out.println();
        modify($claim) {
            setReason($claim + ": failed. Coverage in policy " + $policy + " insufficient."),
            setStatus(Status.DENIED)
        }
end

rule "Invalidate claim with unpaid premiums"
    dialect "java"
    agenda-group "invalid-claim"
    when
        $claim: ClaimDto( status != Status.DENIED )
        $policy: PolicyDto( policyNumber == $claim.getPolicyNumber(), paymentStatus == "Unpaid" )
    then
        System.out.println();
        modify($claim) {
            setReason($claim + ": failed. Unpaid premiums in " + $policy),
            setStatus(Status.DENIED)
        }
end
